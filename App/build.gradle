apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
kapt {
    arguments {
        arg("AROUTER_MODULE_NAME", project.getName())
    }
}

android {
    compileSdkVersion compile_sdk_version
    buildToolsVersion build_tools_version

    defaultConfig {
        applicationId "com.yizhipin"
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode 1 //1.0.0
        versionName "1.0.0"
        flavorDimensions "versionCode" //使用同一维度
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    signingConfigs {
        config {
            keyAlias 'YiZhiPin'
            keyPassword 'YiZhiPin2018'
            storeFile file('D://android/workspace_jianzhi/YiZhiPin_demand/keystore_yizhipin/yizhipin.jks')
            storePassword 'YiZhiPin2018'
        }
    }

    productFlavors {

        YINGYONGBAO {}
        productFlavors.all { flavor ->
            flavor.manifestPlaceholders = [CHANNEL_VALUE: name]
        }
    }

    //修改生成的apk名字
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if ("release".equalsIgnoreCase(variant.buildType.name)) {
                outputFileName = "yzp_android_${defaultConfig.versionName}_${releaseTime()}.apk"
            }
        }
    }
}

def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation project(':Provider')
    if (isUserModule.toBoolean()) {
        implementation project(':UserCenter')
    }
    implementation project(':MessageCenter')
    implementation project(':GoodsCenter')
    implementation project(':OrderCenter')
    implementation project(':PayCenter')
    implementation project(':GeneralizeCenter')

    kapt "com.google.dagger:dagger-compiler:$dagger_version"
}

//Parcelable序列化插件
androidExtensions {
    experimental = true
}

